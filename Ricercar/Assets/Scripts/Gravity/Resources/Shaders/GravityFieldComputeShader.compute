#include "Assets/Scripts/Gravity/Resources/Shaders/GravityShaderResources.cginc"
#pragma kernel ComputeFullField
#pragma kernel ComputeForces
#pragma kernel ComputeSingleBakedAttractorForce

#define THREAD_GROUP_SQRT 16
#define THREAD_GROUP_SIZE 256
#define DEFAULT_BAKED_SIZE 256

// 2d positions where z corresponds to mass of the point
uniform StructuredBuffer<AttractorData> PointAttractors : register(t1);
int PointCount : register(t2);

uniform StructuredBuffer<BakedAttractorData> BakedAttractors : register(t3);
int BakedCount : register(t4);

uniform Texture2DArray<float4> BakedAttractorTextures : register(s1);

RWStructuredBuffer<float2> PointForces;

// the resulting 2d array of gravity vectors,
// the z component represents the average "towardsiness" 
// of each point, i.e. a high negative value means
// the vector is moving away from other points, a high positive
// value means it's moving towards those points
RWTexture2D<float4> GravityFieldOutputTexture;

float2 BottomLeft;
float2 TopRight;

float2 SingleBakedAttractorForceCheckPoint;

// the size of the sample grid
float2 FullFieldSampleSize;

int VisualLayer;

[numthreads(1, 1, 1)]
void ComputeSingleBakedAttractorForce(uint3 id : SV_DispatchThreadID)
{
    float2 position = SingleBakedAttractorForceCheckPoint;

    BakedAttractorData data = BakedAttractors[0];

    float2 origin = float2(data.size, data.size) * 0.5;

    PointForces[id.x] = CalculateGravity(BakedAttractorTextures, data, position + origin, 1, -data.position, position, 1);
}

[numthreads(THREAD_GROUP_SQRT, THREAD_GROUP_SQRT, 1)]
void ComputeFullField(uint3 id : SV_DispatchThreadID)
{
    // get the position in world space
    float2 position = float2(lerp(BottomLeft.x, TopRight.x, id.x / (FullFieldSampleSize.x - 1.0)), lerp(BottomLeft.y, TopRight.y, id.y / (FullFieldSampleSize.y - 1.0)));

    float2 result = float2(0, 0);
    
    for (int i = 0; i < PointCount; i++)
    {
        AttractorData data = PointAttractors[i];

        int interaction = GravityLayerInteraction(VisualLayer, data.layer);
        int doNotCount = data.ignore;

        float2 otherPosition = float2(data.position.x + doNotCount, data.position.y + doNotCount);

        float otherMass = doNotCount + data.mass;
        
        float distance = length(otherPosition - position);

        float2 gravity = CalculateGravity(otherPosition, position, otherMass, 1) / distance;

        float gravityMagnitude = length(gravity);
        float2 gravityDirection = normalize(gravity);

        float shellGravity = lerp(0, data.surfaceGravityForce, distance / max(data.radius, EPSILON));
        int insideShell = when_lt(distance, data.radius);

        gravityMagnitude = lerp(gravityMagnitude, shellGravity, insideShell);

        result += interaction * ((1 - doNotCount) * gravityMagnitude * gravityDirection);
	}
    
    float2 globalOffset = (TopRight + BottomLeft) * 0.5; // centre 
    float2 visualizerSize = float2(TopRight.x - BottomLeft.x, TopRight.y - BottomLeft.y);

    for (int j = 0; j < BakedCount; j++)
    {
        BakedAttractorData data = BakedAttractors[j];
        int interaction = GravityLayerInteraction(VisualLayer, data.layer);

        float2 globalScalar = data.size / visualizerSize;        
    
        float xScalar = data.size / FullFieldSampleSize.x;
        float yScalar = data.size / FullFieldSampleSize.y;

        float2 worldPos = globalOffset - data.position;
        float2 scaledWorldPos = float2(worldPos.x * globalScalar.x, worldPos.y * globalScalar.y);

        result += interaction * CalculateGravity(BakedAttractorTextures, data, float2(id.x * xScalar, id.y * yScalar), globalScalar, scaledWorldPos, position, 1);
	}

    GravityFieldOutputTexture[id.xy] = float4(result, 0, 1);
}
		
[numthreads(1, 1, 1)]
void ComputeForces(uint3 id : SV_DispatchThreadID)
{
    float2 result = float2(0, 0);

    // this kernel computes the attraction from this point to every other point

    AttractorData currentPoint = PointAttractors[id.x];
    float2 position = currentPoint.position;
    float mass = currentPoint.mass;

    for (int i = 0; i < PointCount; i++)
    {
        AttractorData data = PointAttractors[i];

        int interaction = GravityLayerInteraction(currentPoint.layer, data.layer);

        // we dont want to add the gravity 
        // for the attractor to itself. this value returns 1 if the indices are equal,
        // else 0
        // we also use this to disable gravitation towards certain objects
        int doNotCount = or(data.ignore, when_eq(i, id.x));

        // adding the value here to prevent a division by 0. it doesn't matter, this calculation
        // will be discarded anyway
        float2 otherPosition = float2(data.position.x + doNotCount, data.position.y + doNotCount);

        // calculate for lines
        //float2 lineStartPos = float2(otherPoint.lineStartX + doNotCount, otherPoint.lineStartY + doNotCount);
        //float2 lineEndPos = float2(otherPoint.lineEndX + doNotCount, otherPoint.lineEndY + doNotCount);

        //otherPosition = lerp(otherPosition, ProjectPointOnLineSegment(lineStartPos, lineEndPos, position), otherPoint.isLine);
        
        float otherMass = data.mass;

        float2 gravity = CalculateGravity(otherPosition, position, otherMass, mass);

        float gravityMagnitude = length(gravity);
        float2 gravityDirection = normalize(gravity);

        // account for gravity radius: if we're within the radius of the attracting object,
        // force of gravity approaches zero 

        float distance = length(otherPosition - position);

        float shellGravity = lerp(0, data.surfaceGravityForce, distance / max(data.radius, EPSILON)) * mass;
        int insideShell = when_lt(distance, data.radius);

        gravityMagnitude = lerp(gravityMagnitude, shellGravity, insideShell);
        
        result += interaction * ((1 - doNotCount) * gravityMagnitude * gravityDirection);
    }
    
    for (int j = 0; j < BakedCount; j++)
    {
        BakedAttractorData data = BakedAttractors[j];

        int interaction = GravityLayerInteraction(currentPoint.layer, data.layer);

        float2 origin = float2(data.size, data.size) * 0.5;

        // float3 CalculateGravity(Texture2DArray<float4> textures, BakedAttractorData data, float2 uvPosition, float globalScalar, float2 pivot, float2 pointPosition, float pointMass)
        result += interaction * CalculateGravity(BakedAttractorTextures, data, position + origin, 1, -data.position, position, mass);
	}

    PointForces[id.x] = result;
}